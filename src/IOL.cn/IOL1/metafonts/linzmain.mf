"girl";
beginchar("d",xw#,xh#,0);
pickup main_nib;
lft x1=.8curve_sidebar;
w-rt x3=1.2curve_sidebar;   x2=.505w;
bot y1=bot y3=0;   top y2=h+o;
draw z1--z2--z3--z1;
labels(1,2,3); endchar;

"heart, love";
beginchar("H",em#,asc#,desc#);
pickup main_nib;
x0=x3=.5w; lft x1=w-rt x5=curve_sidebar;
x2=w-x4=.5[x1,x3];
bot y0=-d; top y2=top y4=h+o;
y1=y5=.7[y0,y2];
y3=.77[y0,y2];
draw z3..tension 1.6 and 1..z2{left}..z1{down}..{down}z0;
draw z3..tension 1.6 and 1..z4{right}..z5{down}..{down}z0;
labels(0,1,2,3,4,5); endchar;

"bread";
beginchar("B",em#,asc#,desc#);
pickup main_nib;
lft x1=w-rt x3=curve_sidebar;   x2=x5=.5[x1,x3];
x4=w-x6=.35[x1,.5w];
top y2=h+o;   bot y1=bot y3=bot y4=bot y6=-d;
y5=.7h;
draw z1--z3;
draw z1{up}..z2..{down}z3;
draw z4{up}..z5..{down}z6;
labels(1,2,3,4,5,6); endchar;

"speech, talk";
beginchar("T",em#,asc#,desc#);
pickup main_nib;
lft x2=w-rt x3=curve_sidebar;   x1=x3;
top y1=h+o;   bot y3=-d;   y2=.5[y1,y3];
draw z1--z2--z3;   draw z2--(x1,y2);
labels(1,2,3); endchar;

"pen, write";
beginchar("P",em#,asc#,desc#);
pickup main_nib;
lft x2=w-rt x3=curve_sidebar;   x3=x5;
top y3=h+o;   bot y2=bot y4=-d;   y5=.8h;
z5-z4=whatever*(z3-z2);
draw z3--z2--z4--z5;
labels(2,3,4,5); endchar;

"letter";
beginchar("L",em#,asc#,desc#);
pickup main_nib;
lft x1=w-rt x3=straight_sidebar;   x1=x2; x3=x4;
top y1=top y3=h;   bot y2=bot y4=-d;
draw z1--z2--z4--z3--z1;
draw z1--z4;   draw z2--z3;
labels(1,2,3,4); endchar;

"hammer, work";
beginchar("W",em#,asc#,desc#);
pickup main_nib;
lft x1=lft x2=straight_sidebar;   x3=x4=.3w;
rt x0=w-curve_sidebar;   y0=.5[y1,y2];
top y1=top y3=h;   bot y2=bot y4=-d;
draw z1--z2--z4--z3--z1;
draw (x4,y0)--z0;
labels(1,2,3,4); endchar;

