% This is IGREEKL.MF in text format, as of Apr. 22, 1989.
% This lowercase Greek alphabet was prepared by D. E. Knuth in December, 1979,
% inspired by the Monotype faces used in {\sl The Art of Computer Programming}.
% (It has been designed for math formulas, not Greek texts.)
% The programs were revised for the new \MF\ conventions in 1985.
 
% Character codes \0013--\0047 are generated.
 
cmchar "Lowercase Greek beta";
beginchar(ipa_beta,9u#,asc_height#,desc_depth#);
italcorr .5[x_height#,asc_height#]*slant-u#;
adjust_fit(serif_fit#+.5u#,0); pickup tiny.nib;
pos0(stem,180); pos1(stem,180); pos2(vair,90);
pos3(stem,0); pos4(vair,-90); pos5(hair,-180);
pos6(vair,-270); pos7(curve,-360); pos8(vair,-450); pos9(1.25hair,-540);
pos0'(stem,0); pos1'(stem,0); z0'=z0; z1'=z1;
x0=x1=x9; lft x0r=hround u; x4=x6=x8=.5w+.25u; x2=.5w;
rt x3r=hround(w-1.5u); rt x7r=hround(w-1.5u+.5curve); rt x5l=hround(x4-u);
bot y0'=-d; top y6r+.5u=x_height; top y2r=h+oo; y1+o=y3=.5[y2,y4];
y5=.5[y4,y6]; y4l=y6l; bot y8=-oo; y7=y9=.5[y6,y8];
if y6l<y4l: y6l:=y4l:=y5; fi
pos4'(vair,-90); pos6'(vair,-270); z4'=z6'=z5;
filldraw stroke z0e---z1e...pulled_arc.e(2,3)
 & pulled_arc.e(3,4)...{left}z4'e;  % stem and upper bowl
filldraw stroke z6'e{right}...pulled_arc.e(6,7)
 & pulled_arc.e(7,8)...{up}z9e;  % lower bowl
if serifs: dish_serif(0',1',a,1/3,jut,b,1/3,jut); fi
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;

cmchar "Lowercase Greek epsilon";
beginchar(ipa_epsilon,7.25u#+max(.75u#,.5curve#),x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(0,0);
numeric top_thickness,mid_thickness,bot_thickness;
top_thickness=max(fine.breadth,vround(slab-2vair_corr));
mid_thickness=max(fine.breadth,vround 2/3vair);
bot_thickness=max(fine.breadth,vround(slab-vair_corr));
pickup fine.nib; pos2(top_thickness,90); top y2l=h+o;
pos3(max(.6fine.breadth,.35[curve,cap_curve]-stem_corr),180);
pos7(max(.6fine.breadth,.35[curve,cap_curve]-stem_corr),180);
lft x3r=lft x7r=hround max(.5u,1.25u-.5left_curve);
pos4(vair,-90); pos5(vair,-90);
pos8(bot_thickness,-90); bot y8r=-o;
pos9(hair,360);
y9=max(good.y(.5bar_height-.9),y8l+vair); x9r=x1r;
pos6(mid_thickness,90); x2=x6-.5u=x8=.5w+.25u;
y3=.5[top y4l,bot y2l]; y7=.5[bot y6l,top y8l];
lft x5=min(hround 4u,lft x6)-eps;
top y5l=vround(.54h+.5vair); y5r=y6l;
x4=1/3[x5,x3l]; z4=z5+whatever*(150u,h);
filldraw stroke pulled_super_arc.e(2,3)(.5superpull)
 & z3e{down}...z4e---z5e;  % upper bowl
filldraw z5r--z6l--z6r--z5l---cycle;  % middle tip
filldraw stroke pulled_super_arc.e(6,7)(.5superpull)
 & pulled_super_arc.e(7,8)(.5superpull);  % lower bowl
numeric bulb_diam[];
 bulb_diam1=flare-.75(cap_stem-stem); bulb_diam2=flare+cap_stem-stem;
 pos0(bulb_diam1,0); pos1(hair,0);
 rt x0r=hround min(w-.5u,w-u+.5bulb_diam1);
 y0=min(.9h-.5bulb_diam1,.75h+.5bulb_diam1);
 bulb(2,1,0);  % upper bulb
 filldraw stroke term.e(8,9,right,1,4);  % lower terminal
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;
 
cmchar "Closed epsilon";
beginchar(ipa_epsilon_c,8.5u#,x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(0,.5u#);
numeric top_thickness,mid_thickness,bot_thickness;
top_thickness=max(fine.breadth,vround(slab-2vair_corr));
mid_thickness=max(fine.breadth,vround 2/3vair);
bot_thickness=max(fine.breadth,vround(slab-vair_corr));
pickup fine.nib; pos2(top_thickness,90); top y2r=h+o;
pos3(max(.6fine.breadth,.35[curve,cap_curve]-stem_corr),0);
rt x3r=hround(w-u);
pos4(vair,-90); pos5(vair,-90);
pos5'(slab,180); y5'=y5; x5'=0;
pos6(mid_thickness,90); x2=x6=x8=.5[1.5u,x7];
pos7(max(.6fine.breadth,.35[curve,cap_curve]-stem_corr),0);
rt x7r=hround(w-.75u);
lft x5=min(hround 3.5u,lft x6)-eps;
pos8(bot_thickness,-90); bot y8r=-o;
y3=.5[top y4l,bot y2l]; y7=.5[bot y6l,top y8l];
top y5l=vround(.54h+.5vair); y5r=y6l;
x4=1/3[x5,x3l]; z4=z5+whatever*(150u,h);
filldraw stroke z8e{left}...{up}z5'e...z2e
&pulled_super_arc.e(2,3)(.5superpull)
 & z3e{down}...z4e---z5e;  % upper bowl
filldraw z5r--z6l--z6r--z5l---cycle;  % middle tip
filldraw stroke pulled_super_arc.e(6,7)(.5superpull)
 & pulled_super_arc.e(7,8)(.5superpull);  % lower bowl
picture cepsilon;
cepsilon:=currentpicture reflectedabout((.5w,0),(.5w,h));
currentpicture:=cepsilon;
penlabels(2,3,4,5,6,7,8,,5'); endchar;

cmchar "Lowercase Greek reversed epsilon";
beginchar(ipa_epsirev,8.5u#,x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(0,0);
numeric top_thickness,mid_thickness,bot_thickness;
top_thickness=max(fine.breadth,vround(slab-2vair_corr));
mid_thickness=max(fine.breadth,vround 2/3vair);
bot_thickness=max(fine.breadth,vround(slab-vair_corr));
pickup fine.nib; pos2(top_thickness,90); top y2l=h+o;
pos3(max(.6fine.breadth,.35[curve,cap_curve]-stem_corr),0);
rt x3r=hround(w-1.25u);
pos4(vair,-90); pos5(vair,-90);
pos6(mid_thickness,90); x2=x6=x8=.5[1.5u,x7];
pos7(max(.6fine.breadth,.35[curve,cap_curve]-stem_corr),0);
rt x7r=hround(w-.75u);
lft x5=min(hround 4u,lft x6)-eps;
pos8(bot_thickness,-90); bot y8r=-o;
y3=.6[top y4l,bot y2l]; y7=.5[bot y6l,top y8l];
top y5l=vround(.54h+.5vair); y5r=y6l;
x4=1/3[x5,x3l]; z4=z5+whatever*(150u,h);
filldraw stroke pulled_super_arc.e(2,3)(.5superpull)
 & z3e{down}...z4e---z5e;  % upper bowl
filldraw z5r--z6l--z6r--z5l---cycle;  % middle tip
filldraw stroke pulled_super_arc.e(6,7)(.5superpull)
 & pulled_super_arc.e(7,8)(.5superpull);  % lower bowl
numeric bulb_diam[];
 bulb_diam1=flare-.75(cap_stem-stem); bulb_diam2=flare+cap_stem-stem;
 pos0(bulb_diam1,180); pos1(hair,180);
 lft x0r=hround 1.25u; y0=min(.9h-.5bulb_diam1,.75h+.5bulb_diam1);
 bulb(2,1,0);  % upper bulb
 pos9(bot_thickness,angle(-2u,-h));
 lft x9r=hround .75u; top y9l=vround .25h-o;
 y9r:=good.y y9r-eps; x9l:=good.x x9l;
 filldraw stroke term.e(8,9,left,1,4);  % lower terminal
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;
 
cmchar "Right-hook lowercase Greek reversed epsilon";
beginchar(ipa_epsirev_r,11.5u#,x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(0,0);
numeric top_thickness,mid_thickness,bot_thickness;
top_thickness=max(fine.breadth,vround(slab-2vair_corr));
mid_thickness=max(fine.breadth,vround 2/3vair);
bot_thickness=max(fine.breadth,vround(slab-vair_corr));
pickup fine.nib; pos2(top_thickness,90); top y2r=h+o;
pos3(.3[curve,cap_curve]-stem_corr,0);
rt x3r=hround(.78w-1.25u);
pos4(vair,-90); pos5(vair,-90);
pos6(mid_thickness,90); x2=x6=x8=.5[1.5u,x7];
pos7(.3[curve,cap_curve]-stem_corr,0);
rt x7r=hround(.78w-.75u);
lft x5=min(hround 4u,lft x6)-eps;
pos8(bot_thickness,-90); bot y8r=-o;
y3=.6[top y4l,bot y2l]; y7=.5[bot y6l,top y8l];
top y5l=vround(.54h+.5vair); y5r=y6l;
x4=1/3[x5,x3l]; z4=z5+whatever*(150u,h);
filldraw stroke pulled_super_arc.e(2,3)(.5superpull)
 & z3e{down}...z4e---z5e;                             % upper bowl
filldraw z5r--z6l--z6r--z5l---cycle;                  % middle tip
filldraw stroke pulled_super_arc.e(6,7)(.5superpull)
 & pulled_super_arc.e(7,8)(.5superpull);              % lower bowl
numeric bulb_diam[];
 bulb_diam1=flare-.5(cap_stem-stem); bulb_diam2=flare+cap_stem-stem;
 pos0(bulb_diam1,180); pos1(hair,180);
 lft x0r=hround 1.25u; y0=min(.9h-.5bulb_diam1,.75h+.5bulb_diam1);
 bulb(2,1,0);                                         % upper bulb
 pos9(bot_thickness,angle(-2u,-h));
 lft x9r=hround .75u; top y9l=vround .25h-o;
 y9r:=good.y y9r-eps; x9l:=good.x x9l;
 filldraw stroke term.e(8,9,left,1,4);                % lower terminal
%   right hook
pos10(hair,0); pos11(hair,0); pos12(hair,0);
pos13(hair,90); pos14(hair,180);
z10=z3; top y11=h; rt x11r=hround(x3r+2u)=rt x12r;
y12=.65[y11,y13]=y14; x13=.5[x11,x14];
bot y13l=.5h; x14l=w;
filldraw stroke z10e--z11e;
filldraw stroke z11e--z12e...z13e...z14e;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14); endchar;
 
cmchar "Lowercase Greek closed reversed epsilon";
beginchar(ipa_epsirev_c,8.5u#,x_height#,0);
italcorr x_height#*slant-.5u#;
adjust_fit(0,0);
numeric top_thickness,mid_thickness,bot_thickness;
top_thickness=max(fine.breadth,vround(slab-2vair_corr));
mid_thickness=max(fine.breadth,vround 2/3vair);
bot_thickness=max(fine.breadth,vround(slab-vair_corr));
pickup fine.nib; pos2(top_thickness,90); top y2r=h+o;
pos3(max(.6fine.breadth,.35[curve,cap_curve]-stem_corr),0);
rt x3r=hround(w-1.25u);
pos4(vair,-90); pos5(vair,-90);
pos5'(slab,180); y5'=y5; x5'=0;
pos6(mid_thickness,90); x2=x6=x8=.5[1.5u,x7];
pos7(max(.6fine.breadth,.35[curve,cap_curve]-stem_corr),0);
rt x7r=hround(w-.75u);
lft x5=min(hround 4u,lft x6)-eps;
pos8(bot_thickness,-90); bot y8r=-o;
y3=.6[top y4l,bot y2l]; y7=.5[bot y6l,top y8l];
top y5l=vround(.54h+.5vair); y5r=y6l;
x4=1/3[x5,x3l]; z4=z5+whatever*(150u,h);
filldraw stroke z8e{left}...{up}z5'e...z2e
&pulled_super_arc.e(2,3)(.5superpull)
 & z3e{down}...z4e---z5e;  % upper bowl
filldraw z5r--z6l--z6r--z5l---cycle;  % middle tip
filldraw stroke pulled_super_arc.e(6,7)(.5superpull)
 & pulled_super_arc.e(7,8)(.5superpull);  % lower bowl
penlabels(2,3,4,5,6,7,8,,5'); endchar;
 
def dzeta =
italcorr .75[x_height#,asc_height#]*slant-1.5u#;
adjust_fit(0,0); pickup fine.nib;
pos0(hair,-360); pos1(vair,-270); pos2(hair,-180); pos3(vair,-90);
lft x0l=hround 3.5u; x1=x3=.5[x0,x2]; rt x2l=hround max(rt x0r,w-2u)+eps;
top y0=h; top y3l=bot y1l+vstem+eps=vround(y0-.25(h-x_height)); y2=.5[y1,y3];
if y1r>y3r: y1r:=y3r:=y2; fi
lft x4l=hround u; x4r-x4l=(hround .6[vair,stem])-fine;
numeric theta; theta=90-angle(25u,x_height); slope:=-x_height/25u;
pos6(stem,theta);
ellipse_set(3l,4l,5l,6l); ellipse_set(3r,4r,5r,6r);
y4l:=.9[y6,y4l]; y4=y4r:=.9[y6,y4r];
filldraw circ_stroke z0e{down}...z1e{right}...{up}z2e;  % opening hook
enddef;

cmchar "Lowercase Greek zeta";
beginchar(ipa_dzeta,9u#,asc_height#,desc_depth#);
z6=(w-2.5u,-.1d); dzeta;
pos7(vair,-90); x7=w-2u; bot y7r=-d-oo;
pos8(vair,-135); x8=.5w+.75u; y8=-3/4d-oo;
filldraw circ_stroke z8e{x7-x8,2(y7-y8)}...z7e{right}
 ...z6e---z5e....{up}z4e
 ..tension 1 and atleast1.5..{right}z3e...{down}z2e;  % main stroke
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "dz-hook";
beginchar(ipa_dzhook,9u#,asc_height#,desc_depth#);
z6=(w-3.5u,0); dzeta;
filldraw stroke z5e---z6e;
filldraw circ_stroke z5e....{up}z4e
 ..tension 1 and atleast1.5..{right}z3e...{down}z2e;  % main stroke
numeric dot_diam#; dot_diam#=min(.7dot_size#,.7cap_curve#);
define_whole_blacker_pixels(dot_diam);
 pickup tiny.nib; penpos14(x6r-x6l,180); pos6'(stem,180+theta);
 pos15(vair,-90); pos16(hair,0); pos17(flare,0);
 z6=z6';
 z14=(x6,-.5d);
 bot y15r=-d+oo; y16-.5flare=-.75d;
 rt x16r=hround(w-.1u);
 x15=.5[x14,x16];
 filldraw stroke z6'e{down}---z14e{down}..{right}z15e{up};
  bulb(15,16,17); % arc and bulb
penlabels(0,1,2,3,4,5,6,7,8,14,15,16,17); endchar;

cmchar "Lowercase Greek theta";
beginchar(ipa_theta,8.25u#,asc_height#,0);
italcorr .7asc_height#*slant+.5curve#-u#;
adjust_fit(0,0); pickup fine.nib;
pos1(vair,90); pos2(curve,180); pos3(vair,270); pos4(curve,360);
x1=x3=.5w; lft x2r=hround(1.5u-.5curve); x4=w-x2;
top y1r=h+oo; y2=y4=.5[y1,y3]; bot y3r=-oo;
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1);  % bowl
pos2'(bar,90); pos4'(bar,90); z2'=z2l; z4'=z4l;
filldraw stroke z2'e--z4'e;  % bar
penlabels(1,2,3,4); endchar;

cmchar "Lowercase Greek iota";
beginchar(ipa_iota,4.5u#,x_height#,0);
 adjust_fit(if monospace:u#,u# else: u#,0 fi); pickup fine.nib;
x2-.5stem=hround(1.5u-.5stem); x4=w; hook_out(2,3,4);  % closing hook
pickup tiny.nib; pos1(stem,0); pos2'(stem,0); z2'=z2;
top y1=x_height if serifs: +min(oo,serif_drop) fi;
x1=x2; filldraw stroke z1e--z2e;  % stem
sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % serif
penlabels(1,2,3,4); endchar;

def gen_lambda =
adjust_fit(0,0); pickup fine.nib;
pos1(hair,180); pos2(curve,80); pos5(curve,80); pos6(hair,180);
x1-.5hair=hround-.5hair+.5u; x6=w-x1; x2=2u; x5=w-1.5u;
y1=.5[y2l,y3]; top y2r=h+oo; bot y5l=-d-oo; y6=y5r+o;
z0=.5[z2,z5]; z2'=z2l+(.5u,0); z5'=z5r-(.5u,0);
numeric theta; theta=angle(z5'-z2')+90;
pos3(.6stem,theta); pos4(.6stem,theta);
y3r=y2r-h/4; y4l=y5l+h/4;
x3+1.25u=x4-1.25u=x0;
filldraw stroke z1e{up}...z2e{right}...z3e{z4r-z3l}
 ..z4e{z4r-z3l}...{right}z5e...{up}z6e;  % major diagonal and hooks
%y15=.5[bar_height,x_height];
y15=.55h;
z15=whatever[z3,z4];
x16=1.5u; y16-.5stem=-oo;
pos15(hair,theta-90); pos16(stem,angle(z15-z16)-90);
filldraw circ_stroke z16e--z15e; % short diagonal
penlabels(1,2,2',3,4,5,5',6,15,16); enddef;

cmchar "Lowercase Greek lambda";
beginchar(ipa_dl,9.5u#,asc_height#,0);
gen_lambda; endchar;
 
cmchar "Lowercase crossed Greek lambda";
beginchar(ipa_tl,9.5u#,asc_height#,0);
gen_lambda; pickup fine.nib;
pos7(bar,125); pos8(bar,125);
x7=x1; y7 = .57h; top y8=.9h;
x8=.5[x5,x4];
filldraw stroke z7e--z8e; % crossbar
penlabels(7,8); endchar;

cmchar "Lowercase Greek pi";
beginchar(ipa_pi,10u#,x_height#,0);
italcorr x_height#*slant+.5stem#-u#;
adjust_fit(0,0); pi_stroke; % hook and bar
pos4(hair,0); pos6(hair,0); x4=3.5u; x6=w-4u; y4=y6=y2;
x5=3u; x7=w-2.5u; y5=y7=.5stem-oo;
pair v[]; v1=(z4-z5) xscaled 3.14159; v2=(z6-z7) xscaled 3.14159;
pos5(stem,angle v1-90); pos7(stem,angle v2-90);
filldraw circ_stroke z5e{v1}...{up}z4e;  % left stem
filldraw circ_stroke z7e{v2}...{up}z6e;  % right stem
penlabels(1,2,3,4,5,6,7); endchar;

cmchar "Variant lowercase Greek rho";
beginchar(ipa_rho,8.25u#,x_height#,desc_depth#);
italcorr .7x_height#*slant+.5curve#-u# if math_fitting:-.5u# fi;
adjust_fit(0,0); pickup fine.nib;
pos2(stem,180); pos3(vair,90); pos4(curve,0); pos5(vair,-90); pos6(stem,-180);
lft x2r=hround(1.5u-.5stem); x2=x6; x3=x5=.5[x2,x4];
rt x4r=hround(w-1.5u+.5curve);
y2=y4=y6=.5[y3,y5]; top y3r=h+oo; bot y5r=-oo;
filldraw circ_stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
 & pulled_arc.e(4,5) & pulled_arc.e(5,6);  % bowl
pos2'(hair,-180); z2'r=z2r; pos1(vstem,-90); pos1'(vstem,90); z1'=z1;
pos0(hair,0); rt x0r=hround(w-u); bot y0=-d; bot y1r=vround-.9d; x1=x5;
interim superness:=sqrt superness;
filldraw stroke pulled_arc.e(0,1'); filldraw stroke pulled_arc.e(1,2'); % tail
penlabels(0,1,2,3,4,5,6); endchar;

cmchar "Script v (Lowercase Greek upsilon)";
beginchar(ipa_vappr,9u#,x_height#,0);
italcorr x_height#*slant;
adjust_fit(0,0);
pickup tiny.nib; interim superness:=more_super;
pos3(stem,-180); lft x3r=hround(2.5u-.5stem);
x0=0; x2=x3; y2=h;
pos2'(stem,-180); z2'=z2; pos4(vair,-90); pos5(hair,0);
pos7(stem,0); pos8(stem,0);
x7=x2; top y7=h+min(oo,serif_drop); z8=(x3,.5bar_height);
x4=.5w; rt x5r=hround(w-.5u); y3=.5[y4,y5]; bot y4r=-oo; y5=.57h;
filldraw stroke z2'e{down}--super_arc.e(3,4)
 ..tension atleast 1.05..{up}z5e; % left stem and arc
v_bulb(5,6);  % closing bulb
pickup fine.nib; sloped_serif.l(7,8,a,1/3,jut,serif_drop);
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "Inverted script v (Lowercase Greek upsilon)";
beginchar(ipa_vappr_r,9u#,x_height#,0);
italcorr x_height#*slant;
adjust_fit(0,0);
pickup tiny.nib; interim superness:=more_super;
pos3(stem,0); rt x3r=hround(w-2.5u+.5stem);
x0=w; x2=x3; y2=0;
pos2'(stem,0); z2'=z2; pos4(vair,90); pos5(hair,180);
pos7(stem,0); pos8(stem,0);
x7=x2; bot y7=-min(oo,serif_drop); z8=(x3,.5[bar_height,h]);
x4=.5w; lft x5r=hround(.5u); y3=.5[y4,y5]; top y4r=h+oo; y5=.43h;
filldraw stroke z2'e{up}--super_arc.e(3,4)
 ..tension atleast 1.05..{down}z5e; % right stem and arc
y6-.5curve=-oo; x6-.5curve=u;
numeric theta; theta=angle(4(x5-x6),y5-y6); pos6(curve,theta+90);
filldraw z6l{dir theta}..tension atleast 1 and 1..{up}z5l
 --z5r{down}...{-dir theta}z6r..cycle;  % bulb
pickup fine.nib; sloped_serif.r(7,8,a,1/3,jut,min(oo,serif_drop));
penlabels(0,1,2,3,4,5,6,7,8); endchar;

cmchar "Lowercase Greek chi";
beginchar(ipa_chi,9.5u#,x_height#,desc_depth#);
italcorr x_height#*slant+.5hair#-1.5u#;
adjust_fit(0,0); pickup fine.nib;
pos1(hair,180); pos2(curve,80); pos5(curve,80); pos6(hair,180);
x1-.5hair=hround-.5hair+.5u; x6=w-x1; x2=w-x5=2u;
y1=y2l-h/6; top y2r=h+oo; bot y5l=-d-oo; y6=y5r+h/6;
z0=.5[z2,z5]; z2'=z2l+(.5u,0); z5'=z5r-(.5u,0);
numeric theta; theta=angle(z5'-z2')+90;
pos3(hair,theta); pos4(hair,theta);
y3r=y2r-h/3; y4l=y5l+h/3; %z3l=whatever[z2',z0]; z4r=whatever[z0,z5'];
x3r+u=x4l-u=.5w;
filldraw stroke z1e{up}...z2e{right}...z3e{z4r-z3l}
 ..z4e{z4r-z3l}...{right}z5e...{up}z6e;  % major diagonal and hooks
x8l=w-x7r=u; top y7=h; bot y8=-d;
%numeric theta; theta=angle(z7-z8)-90;
pos7(stem,0); pos8(stem,0);
filldraw stroke z7e--z8e;  % minor diagonal
penlabels(0,1,2,2',3,4,5',5,6,7,8); endchar;
 
cmchar "omega (rnd bck sml)";
beginchar(ipa_omega,11u#,x_height#,0);
italcorr x_height#*slant;
adjust_fit(0,0); pickup fine.nib;
pos2(hair,-180); lft x2r=hround.5u; y2=y4=y6=y8=bar_height;
y1+.5hair=h; x1=x2+.75u; pos1(hair,angle(2(x1-x2),y1-y2)+90);
pos3(vstem,-90); pos4(hair,0); pos5(vair,90);
pos6(hair,180); pos7(vstem,270); pos8(hair,360);
bot y3r=bot y7r=-oo; top y5r=vround(.2[y4,h]+.5vair);
x3=.4[x2,x4]; x7+.25u=.6[x6,x8]; rt x8r=hround(w-.5u);
x5+.5stem=hround(.5w+.5stem)+eps;
x5=.5[x4,x6]; rt x4r-lft x6r=min(stem,2hair)+2eps;
if x4l<x6l: x4l:=x6l:=x5; fi
filldraw circ_stroke z1e{2(x2-x1),y2-y1}...z2e{down}...z3e{right}
 ...{up}z4e...{left}z5e;  % left arc
filldraw stroke z5e{left}...z6e{down}...z7e{right}...{up}z8e;  % right arc
v_bulb(8,9);  % closing bulb
penlabels(1,2,3,4,5,6,7,8,9); endchar;
 
cmchar "Closed lowercase Greek omega";
beginchar(ipa_clomega,11u#,x_height#,0);
italcorr x_height#*slant;
adjust_fit(0,0); pickup fine.nib;
pos2(curve,-180); lft x2r=hround.5u; y2=y8=.45x_height;
y4=y6=vround.6h;
%y1+.5hair=h; x1=x2+.75u; pos1(hair,angle(2(x1-x2),y1-y2)+90);
y1r=h+oo; x1=.5w; pos1(hair,90);
pos3(hair,-90); pos4(hair,0); pos5(vair,90);
pos6(hair,180); pos7(hair,270); pos8(curve,360);
bot y3r=bot y7r=-oo; top y5r=vround(.1[y4,h]+.5vair);
x3=.5[x2,x4]; x7+.25u=.5[x6,x8]; rt x8r=hround(w-.5u);
x5+.5stem=hround(.5w+.5stem)+eps;
x5=.5[x4,x6]; rt x4r-lft x6r=min(stem,2hair)+2eps;
if x4l<x6l: x4l:=x6l:=x5; fi
filldraw circ_stroke z1e{left}...z2e{down}...z3e{right}
 ...{up}z4e...{left}z5e;  % left arc
filldraw stroke z5e{left}...z6e{down}...z7e{right}...{up}z8e...z1e{left};
penlabels(1,2,3,4,5,6,7,8); endchar;

cmchar "agemo (rnd cnt sml)";
beginchar(ipa_agemo,11u#,x_height#,0);
italcorr x_height#*slant;
adjust_fit(0,0); pickup fine.nib;
pos2(hair,-180); lft x2r=hround.5u; y2=y4=y6=y8=bar_height;
y1+.5hair=h; x1=x2+.75u; pos1(hair,angle(2(x1-x2),y1-y2)+90);
pos3(vstem,-90); pos4(hair,0); pos5(vair,90);
pos6(hair,180); pos7(vstem,270); pos8(hair,360);
bot y3r=bot y7r=-oo; top y5r=vround(.2[y4,h]+.5vair);
x3=.4[x2,x4]; x7+.25u=.6[x6,x8]; rt x8r=hround(w-.5u);
x5+.5stem=hround(.5w+.5stem)+eps;
x5=.5[x4,x6]; rt x4r-lft x6r=min(stem,2hair)+2eps;
if x4l<x6l: x4l:=x6l:=x5; fi
filldraw circ_stroke z1e{2(x2-x1),y2-y1}...z2e{down}...z3e{right}
 ...{up}z4e...{left}z5e;  % left arc
filldraw stroke z5e{left}...z6e{down}...z7e{right}...{up}z8e;  % right arc
v_bulb(8,9);  % closing bulb
picture rvchar;
if slant=0: rvchar=currentpicture rotatedabout((.5w,.5h),180);%
  else: rvchar=currentpicture rotatedabout((.58w,.5h),180);fi
currentpicture:= rvchar;
penlabels(1,2,3,4,5,6,7,8,9); endchar;
 
cmchar "The letter baby gamma";
beginchar(ipa_baby_gamma,if serifs: 9.5u# else:9u# fi,x_height#,0);
italcorr x_height#*slant-.05u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
numeric stem[],outer_jut,xjut,alpha[];
stem1=fudged.stem-4stem_corr; stem2=min(1.5fudged.hair,stem1);
outer_jut=.75jut; xjut= if serifs: (stem1-stem2)/4 else: 0 fi;
x1l=l+letter_fit+.1u+outer_jut; x2r=r-letter_fit-.3u-outer_jut-xjut;
x3l=l+letter_fit+2.5outer_jut+xjut; x4r=r-letter_fit-2outer_jut;
y1=y2=h; y3=y4=.3h; z11=z3;
x10=.5[x3,x4]; y10r=0;
alpha1=diag_ratio(1,stem1,h,x4r-x1l);
alpha2=diag_ratio(1,stem2,h,x2r-x3l);
penpos1(alpha1*stem1,0); penpos2(alpha2*stem2,0);
penpos3(alpha2*stem2,0); penpos4(alpha1*stem1,0);
penpos10(alpha2*stem2,-90); penpos11(alpha2*stem2,180);
if hefty: z0=whatever[z1,z4]=whatever[z2,z3];
 x12=x34=x0; y13=y24=y0;
 z12=whatever[z2l,z3l]; z13=whatever[z2l,z3l];
 z24=whatever[z2r,z3r]; z34=whatever[z2r,z3r];
 forsuffixes $=13,24,34: z$'=.2[z$,z0]; endfor
 fill diag_end(12,1r,.5,1,1l,13')--z13'--diag_end(13',3l,1,.5,3r,34')--z34'
  --diag_end(34',4l,.5,1,4r,24')--z24'
  --diag_end(24',2r,1,.5,2l,12)--z12--cycle; % diagonals
else: fill diag_end(4r,1r,.5,1,1l,4l)
  --diag_end(1l,4l,.5,1,4r,1r)--cycle; % left diagonal
 fill diag_end(2l,3l,.5,1,3r,2r)
  --diag_end(3r,2r,.5,1,2l,3l)--cycle; fi  % right diagonal
filldraw stroke pulled_arc.e(4,10) & pulled_arc.e(10,11);% lower bowl
if serifs: numeric inner_jut[]; pickup tiny.nib;
 prime_points_inside(1,4); prime_points_inside(2,3);
 prime_points_inside(3,2); prime_points_inside(4,1);
 if rt x1'r+jut+.5u+1<=lft x2'l-jut-xjut: inner_jut1=jut;
 else: rt x1'r+inner_jut1+.5u+1=lft x2'l-inner_jut1-xjut; fi
 if rt x3'r+jut+.5u+1<=lft x4'l-jut-xjut: inner_jut2=jut;
 else: rt x3'r+inner_jut2+.5u+1=lft x4'l-inner_jut2-xjut; fi
 dish_serif(1',4,a,1/3,outer_jut,b,2/3,inner_jut1);  % upper left serif
 dish_serif(2',3,e,2/3,inner_jut1+xjut,
  f,1/2,outer_jut+xjut)(dark); fi % upper right serif
penlabels(0,1,2,3,4,10,12,13,24,34); endchar;
 
cmchar "Lowercase Greek phi";
beginchar(ipa_phi,11u#,asc_height#,desc_depth#);
italcorr .75x_height#*slant-.5u#;
adjust_fit(serif_fit#,0);
pickup tiny.nib;
penpos1(stem'-tiny,0); penpos2(stem-tiny,0);
pos0'(stem',0); pos0(stem,0);z0r=z0'r; x0'=x1; x0=x2;
lft x1l=hround(.5w-.5stem'); top y1=h if serifs: +min(oo,serif_drop) fi;
penpos3(cap_band,90); penpos4(cap_band,90); penpos5(curve,0);
penpos6(.5[vair,cap_band],-90); penpos7(.5[vair,cap_band],-90);
penpos8(cap_band,90); penpos9(curve,180);
penpos10(.5[vair,cap_band],270);
x3=x1=x7; y4r=y8r=x_height+o; y5=y9=.5[y4l,y6l];
y7=y6+u; x4r=x6r=w-x8r=w-x10r=.5w+1.75u;
x5r=w-x9r=hround(w-.5u); y3=y4-u; y6r=y10r=-o;
numeric edge; edge=rt x2r;
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{right}z4l));
pickup tiny.nib; bot y2=-d;
filldraw stroke z1e--z0e--z0'e--z2e; % stem
fill stroke z3e..pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;
fill stroke z3e..pulled_arc.e(8,9) & pulled_arc.e(9,10)..z7e;  % lobe
if serifs:
 sloped_serif.l(1,0',a,1/3,jut,serif_drop);  % upper serif
 dish_serif(2,0,b,1/3,jut,c,1/3,jut); fi  % lower serif
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

cmchar "Lowercase Greek psi";
beginchar(ipa_psi,11u#,asc_height#,desc_depth#);
italcorr x_height#*slant;
adjust_fit(0,0);
penpos1(stem-tiny,0); penpos2(stem-tiny,0); x1=x2=x14;
top y1=h if serifs: +min(oo,serif_drop) fi;
pickup tiny.nib; interim superness:=more_super;
pos13(stem,-180); lft x13r=hround(2.5u-.5stem);
x10=0; x12=x13; y12=x_height;
pos12'(stem,-180); z12'=z12; pos14(vair,-90); pos15(hair,0);
pos17(stem,0); pos18(stem,0);
x17=x12; top y17=x_height+min(oo,serif_drop); z18=(x13,.5bar_height);
x14=.5(w+u); rt x15r=hround(w-.5u);
y13=.5[y14,y15]; bot y14r=-oo; y15=.57x_height;
filldraw stroke z12'e{down}--super_arc.e(13,14)
 ..tension atleast 1.05..{up}z15e; % left stem and arc
v_bulb(15,16);  % closing bulb
pickup fine.nib; sloped_serif.l(17,18,u,1/3,jut,serif_drop);
pickup tiny.nib; bot y2=-d;
filldraw stroke z1e--z2e; % stem
if serifs:
 sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % upper serif
 dish_serif(2,1,b,1/3,jut,c,1/3,jut); fi  % lower serif
penlabels(1,2,10,11,12,13,14,15,16,17,18); endchar;

